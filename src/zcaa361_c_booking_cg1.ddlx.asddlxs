@Metadata.layer: #CORE
@UI: {
  headerInfo: { typeName: 'Booking',
                typeNamePlural: 'Bookings',
                title: { type: #STANDARD, value: 'BookingID' } } }
annotate view ZCAA361_C_Booking_CG1 with
{
  @UI.facet: [ { id:            'Booking',
                 purpose:       #STANDARD,
                 type:          #IDENTIFICATION_REFERENCE,
                 label:         'Booking',
                 position:      10 } ]
  TravelID;
  @UI: { lineItem:       [ { position: 20, importance: #HIGH } ],
         identification: [ { position: 20 } ] }
  BookingID;
  @UI: { lineItem:       [ { position: 30, importance: #HIGH } ],
  identification: [ { position: 30 } ] }
  BookingDate;
  @UI: { lineItem:       [ { position: 40, importance: #HIGH } ],
         identification: [ { position: 40 } ] }
  @Consumption.valueHelpDefinition: [{entity: {name: '/DMO/I_Customer', element: 'CustomerID' }}]
  CustomerID;
  @UI: { lineItem:       [ { position: 50, importance: #HIGH } ],
         identification: [ { position: 50 } ] }
  @Consumption.valueHelpDefinition: [{entity: {name: '/DMO/I_Carrier', element: 'AirlineID' }}]
  CarrierID;
  @UI: { lineItem:       [ { position: 60, importance: #HIGH } ],
         identification: [ { position: 60 } ] }
  @Consumption.valueHelpDefinition: [ {entity: {name: '/DMO/I_Flight', element: 'ConnectionID'},
                                       additionalBinding: [ { localElement: 'FlightDate',   element: 'FlightDate'},
                                                            { localElement: 'CarrierID',    element: 'AirlineID'},
                                                            { localElement: 'FlightPrice',  element: 'Price'},
                                                            { localElement: 'CurrencyCode', element: 'CurrencyCode' } ] } ]
  ConnectionID;
  @UI: { lineItem:       [ { position: 70, importance: #HIGH } ],
         identification: [ { position: 70 } ] }
  @Consumption.valueHelpDefinition: [ {entity: {name: '/DMO/I_Flight', element: 'FlightDate' },
                                       additionalBinding: [ { localElement: 'ConnectionID', element: 'ConnectionID'},
                                                            { localElement: 'CarrierID',    element: 'AirlineID'},
                                                            { localElement: 'FlightPrice',  element: 'Price' },
                                                            { localElement: 'CurrencyCode', element: 'CurrencyCode' }]}]
  FlightDate;
  @UI: { lineItem:       [ { position: 80, importance: #HIGH } ],
         identification: [ { position: 80 } ] }
  FlightPrice;
  @Consumption.valueHelpDefinition: [{entity: {name: 'I_Currency', element: 'Currency' }}]
  CurrencyCode;
  @UI.hidden: true
  LastChangedAt; -- Take over from parent
}